{
  parserClass = 'com.rahulrav.baseline.Parser'
  extends = 'com.intellij.extapi.psi.ASTWrapperPsiElement'
  psiPackage = 'com.rahulrav.baseline.psi'
  psiImplPackage = 'com.rahulrav.baseline.psi.impl'
  psiClassPrefix = 'B'
  psiImplClassSuffix = 'Impl'
  tokenTypeClass = 'com.rahulrav.baseline.psi.Token'
  elementTypeClass = 'com.rahulrav.baseline.psi.Element'
  elementTypeHolderClass = 'com.rahulrav.baseline.psi.TokenTypes'
  psiImplUtilClass = 'com.rahulrav.baseline.psi.impl.PsiImplUtils'
  extends('.*stmt$')=statement
  tokens = [
    SLASH = '/'
    SEMI = ';'
    AP = '['
    WILDCARD = '*'
    LT = '<'
    GT = '>'
    LP = '('
    RP = ')'
    SPACE = ' '
    COMMENT_P = 'regexp:[#]'
    ID = 'regexp:[a-zA-Z_$0-9?-]'
    NEWLINE = 'regexp:\r\n|\r|\n'
    ANY = 'regexp:.'
  ]
}

// https://docs.oracle.com/javase/specs/jvms/se7/html/jvms-4.html

root ::= statement*

statement ::=
    class_stmt
    | method_stmt
    | empty_stmt
    | comment_stmt

class_stmt ::= class_rule NEWLINE
class_rule ::= [
    basic_class_rule |
    class_prefix path_component SLASH match_all SEMI |
    class_prefix path_component SLASH WILDCARD SEMI
]

basic_class_rule ::= class_prefix path_component SEMI
path_component ::= component (SLASH component)*
component ::= ID+
class_prefix ::= 'L'

array_rule ::= AP [basic_class_rule | primitives]
argument_rule ::= match_no_args | match_all_args | LP type (type)* RP
// Types should only contain the basic class rule.
// Otherwise it gets very ambiguous
type ::= array_rule | basic_class_rule | primitives
primitives ::= 'Z' | 'V' | 'B' | 'S' | 'I'| 'J'| 'F'| 'D' | 'C'

method_stmt ::= flags method_class_rule method_name argument_rule return_type? NEWLINE
method_class_rule ::= [
    class_prefix path_component match_all arrow |
    class_prefix path_component SEMI arrow |
    class_prefix path_component SLASH match_all arrow |
    class_prefix path_component SLASH WILDCARD arrow
]
method_name ::= match_all | method_component
method_component ::= [ID+ WILDCARD ID+ | ID+ WILDCARD | LT component GT | component]
return_type ::= match_all | type

arrow ::= '-' GT
match_all ::= WILDCARD WILDCARD
match_no_args ::= LP RP
match_all_args ::= LP WILDCARD WILDCARD RP
flags ::= 'HSP' | 'HS' | 'HP' | 'SP' | 'P' | 'S' | 'H'

comment_stmt ::= COMMENT_P (SPACE | ID | any_char)* NEWLINE
any_char ::= ANY | SLASH | SEMI | WILDCARD | AP | LT | GT | LP | GP | COMMENT_P
empty_stmt ::= NEWLINE
